plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id "com.github.breadmoirai.github-release" version "2.4.1"
}

def getVersionSuffix() {
	// CI Environment
	if (System.getenv("GITHUB_ACTIONS")) {
		def ref = System.getenv("GITHUB_REF")
		if (ref == "refs/heads/main" || (ref != null && ref.startsWith("refs/tags/"))) {
			return ""
		}
		return "-SNAPSHOT"
	}

	// Local Environment
	try {
		def process = "git rev-parse --abbrev-ref HEAD".execute()
		process.waitFor()
		if (process.exitValue() == 0) {
			def branch = process.text.trim()
			if (branch == "main") return ""
		}
	} catch (Exception ignored) {}

	return "-SNAPSHOT"
}

version = project.mod_version + getVersionSuffix()
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

    maven {url = "https://maven.shedaniel.me/" }  // Cloth Config
    maven {url = "https://maven.terraformersmc.com/" }  // ModMenu
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"screenshot-manager" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    // TODO: make the below mappings work to be forward compatible.
    //	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // NOTE: Use https://linkie.shedaniel.dev/ to manage version compatibility

    // Cloth Config - The 'exclude' prevents duplicate Fabric API loading
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
      }
    // ModMenu
    modImplementation("com.terraformersmc:modmenu:${modmenu_version}")


    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // Ensure dependencies are refreshed
}

test {
    useJUnitPlatform()
}


processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

githubRelease {
	token = System.getenv("GITHUB_TOKEN") ?: "" // Prevent null ambiguity
	owner = "milezerosoftware"
	repo = "screenshot-manager-enhanced"
	tagName = "v${project.version}"
	targetCommitish = "main"
	releaseName = "v${project.version}"
	body = "" 
	draft = false
	prerelease = false
	releaseAssets = files("build/libs/${project.archives_base_name}-${project.version}.jar")
	overwrite = true
}