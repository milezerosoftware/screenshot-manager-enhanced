plugins {
    id 'fabric-loom'
    id 'maven-publish'
}

version = "${project.mod_version}-${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.terraformersmc.com/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Cloth Config
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    // ModMenu
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
}

loom {
    splitEnvironmentSourceSets()
    
    mods {
        "screenshot-manager-enhanced" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

// Map the common source into this project
sourceSets.main.java.srcDirs += [project(":common").file("src/main/java")]
sourceSets.main.resources.srcDirs += [project(":common").file("src/main/resources")]
sourceSets.client.java.srcDirs += [project(":common").file("src/client/java")]
sourceSets.client.resources.srcDirs += [project(":common").file("src/client/resources")]

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
               "minecraft_version": project.minecraft_version,
               "loader_version": project.loader_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = Integer.parseInt(project.java_version)
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)
}
